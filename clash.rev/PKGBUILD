# Maintainer: irmluity <45vw4yz8g@mozmail.com>

pkgname=clash.rev
pkgver=1.0.2
pkgrel=1
pkgdesc="Continuation of Clash core project"
arch=("x86_64")
url='https://github.com/MerlinKodo/clash-rev'
license=('GPL3')
depends=()
optdepends=('clash-geoip: A GeoLite2 data created by MaxMind')
makedepends=('git' 'go' 'cmake' 'zip')
conflicts=(${pkgname}-bin ${pkgname}-git 'clash')
options=(!strip)
source=(
    "https://github.com/MerlinKodo/clash-rev/archive/refs/tags/v${pkgver}.zip"
    "clash.service"
)
sha256sums=(
    "d8b8f42d312d448498e21b7d0c5cbba4142e7be72329381109c060a71a431f59"
    "8fdf29dc6d2ee076c88f63ed3224d2a669b1ce1f6de6d7f2fa1f7899d853f1f8"
)

build() {
    cd "${srcdir}/clash-rev-${pkgver}"
    git init
    make -j $(go run ./test/main.go) linux-amd64
    mv bin/clash.rev-linux-amd64 "${srcdir}/${pkgname}-${CARCH}-${pkgver}"
}

package() {
    cd "${srcdir}"
    install -Dm755 "${srcdir}/${pkgname}-${CARCH}-${pkgver}" "${pkgdir}/usr/bin/clash"
    install -Dm644 "clash.service" "${pkgdir}/usr/lib/systemd/system/clash.service"
}