# Maintainer: irmluity <45vw4yz8g@mozmail.com>

_pkgname=clash.rev
pkgname=${_pkgname}-git
pkgver=1.0.2
pkgrel=1
pkgdesc="Continuation of Clash core project"
arch=("x86_64")
url='https://github.com/MerlinKodo/clash-rev'
license=('GPL3')
depends=()
optdepends=('clash-geoip: A GeoLite2 data created by MaxMind')
makedepends=('git' 'go' 'cmake' 'zip')
provides=(${_pkgname})
conflicts=(${_pkgname} ${_pkgname}-git 'clash')
options=(!strip)
source=(
    "git+https://github.com/MerlinKodo/clash-rev.git"
    "clash.service"
)
sha256sums=(
    "SKIP"
    "8fdf29dc6d2ee076c88f63ed3224d2a669b1ce1f6de6d7f2fa1f7899d853f1f8"
)


pkgver() {
    cd "$srcdir/clash-rev"
    printf "%s" "$(git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/^v//')"
}

build() {
    cd "${srcdir}/clash-rev"
    make -j $(go run ./test/main.go) linux-amd64
    mv bin/clash.rev-linux-amd64 "${srcdir}/${_pkgname}-${CARCH}-${pkgver}"
}

package() {
    cd "${srcdir}"
    install -Dm755 "${_pkgname}-${CARCH}-${pkgver}" "${pkgdir}/usr/bin/clash"
    install -Dm644 "clash.service" "${pkgdir}/usr/lib/systemd/system/clash.service"
}